plugins {
  id "org.xtext.xtend" version "1.0.0"
}

repositories {
  jcenter() //needed for xtext/xtend
  ivy {
    url = 'https://www.mirrorservice.org/sites/download.eclipse.org/eclipseMirror/modeling/mdt/uml2/updates/5.0/R201502020947/plugins/'
    layout 'pattern', {
      artifact '[artifact](_[revision])(.[ext])'
    }
  }
  maven {
    url = 'https://jitpack.io'
  }
}

//To use intellij you need xtend plugin -- https://eclipse.org/xtend/download.html

dependencies {
  //emf packages needed for org.eclipse.uml2
  compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.common', 				version: '2.11.0-v20150123-0347'
  compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore',				version: '2.11.0-v20150123-0347'
  compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi', 			version: '2.11.0-v20150123-0347'
  compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.mapping.ecore2xml', 	version: '2.8.0-v20150123-0452'

  //uml2 packages
  //TODO: eclipse plugin is not able to load the sources from the repository
  compile group: 'org.eclipse.uml2', name: 'org.eclipse.uml2.common', version: '2.0.1.v20150202-0947', ext: 'jar'
  compile group: 'org.eclipse.uml2', name: 'org.eclipse.uml2.types', version: '2.0.0.v20150202-0947', ext: 'jar'
  compile group: 'org.eclipse.uml2', name: 'org.eclipse.uml2.uml', version: '5.0.2.v20150202-0947', ext: 'jar'
  compile group: 'org.eclipse.uml2', name: 'org.eclipse.uml2.uml.profile.standard', version: '1.0.0.v20150202-0947', ext: 'jar'
  compile group: 'org.eclipse.uml2', name: 'org.eclipse.uml2.uml.resources', version: '5.0.2.v20150202-0947', ext: 'jar'

  compile group: 'org.eclipse.xtend', name: 'org.eclipse.xtend.lib', version: '2.9.0'

  compile group: 'com.github.petitparser', name: 'java-petitparser', version: '2.0.0'
}

compileJava {
  options.encoding = "Cp1252" //we need this for the '<<' '>>' guillemets in the xtend source code
}
eclipseJdt << {
  File f = file('.settings/org.eclipse.core.resources.prefs')
  f.write('eclipse.preferences.version=1\n')
  f.append('encoding/<project>=Cp1252\n')
}

xtextEclipseSettings << {
  File ioFile = file('.settings/org.eclipse.xtend.core.Xtend.prefs')
  Properties properties = new Properties()
  properties.load(ioFile.newReader())
  properties.setProperty('outlet.DEFAULT_OUTPUT.userOutputPerSourceFolder', 'false')
  properties.setProperty('outlet.DEFAULT_OUTPUT.directory', 'main')
  properties.store(ioFile.newWriter(), null)
}

//To run the Main from gradle do --> ./gradlew :tuml-interpreter:run
//To build a distribution of this project --> ./gradlew :tuml-interpreter:assembleDist
apply plugin: 'application'
mainClassName = "tuml.interpreter.Main"


